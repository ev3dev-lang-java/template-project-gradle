//Gradle file library ev3dev-lang-java // Template project

version = '0.3.0'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

//Wrapper
task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '4.7'
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    compile("ch.qos.logback:logback-classic:1.2.3")
    compile("com.github.ev3dev-lang-java:ev3dev-lang-java:2.2.3")
}

//Compile
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

//Jar
jar {
    baseName = "${rootProject.name}"
    manifest {
        from file("${projectDir}/src/main/resources/META-INF/MANIFEST.MF")
    }
}

//Fat Jar
task fatJar(type: Jar) {
    baseName = "${rootProject.name}" + "-all"
    manifest {
        from file("${projectDir}/src/main/resources/META-INF/MANIFEST.MF")
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

//Deploy on Brick
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:2.0.0"
    }
}
apply plugin: 'org.hidetake.ssh'

apply from: './gradle/deploy.gradle'
apply from: './gradle/installer.gradle'
